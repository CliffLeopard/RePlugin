/**
 * 支持一键Push到Maven上，方便公司开发人员直接使用
 * 注意：除版本号外，请不要随意改动该文件，否则有可能导致覆盖线上Maven库，引发一些事故
 *
 * @author Jiongxuan Zhang
 */
apply plugin: 'maven-publish'

void toDependency(org.gradle.api.publish.maven.MavenPom pom, Configuration c) {
    // 默认情况下不会将Dependencies写入POM，这段代码负责写
    pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')
        toDependencyImpl(dependenciesNode, configurations.compile)
        if (c != null) {
            toDependencyImpl(dependenciesNode, c)
        }
    }
}

static void toDependencyImpl(def dependenciesNode, Configuration c) {
    c.allDependencies.each { dependency ->
        if (dependency.name != "unspecified") {   // 忽略fileTree等属性，只管Maven引用
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', dependency.group)
            dependencyNode.appendNode('artifactId', dependency.name)
            dependencyNode.appendNode('version', dependency.version)
        }
    }
}

// Snapshot和Release的上传策略（URL、VersionName加后缀）不同，需区别对待
// Added by Jiongxuan Zhang
def isSnapshot = config.isSnapshot == "true"
def repoUrl = isSnapshot ? config.repoSnapshot : config.repoPublish

publishing {
    repositories {
        maven {
            url repoUrl
            credentials {
                username = config.username
                password = config.password
            }
        }
    }
    publications {
        // 需要分开发布dev和publish，这样宿主可以按需使用
        // 同时也解决了：multiple artifacts with the identical extension and classifier ('aar', 'null').
        dev(MavenPublication) {
            groupId config.groupId
            artifactId "$config.artifactId-dev"
            version android.defaultConfig.versionName
            // NOTE 不允许上传源代码和JavaDoc！！！
            // artifact(sourceJar)
            // artifact(javadocsJar)
            artifact("$buildDir/outputs/aar/$artifactId-debug.aar")
            // 默认情况下不会将Dependencies写入POM，这段代码负责写
            toDependency(getPom(), configurations.devCompile);
        }
        publish(MavenPublication) {
            groupId config.groupId
            artifactId "$config.artifactId-publish"
            version android.defaultConfig.versionName
            // NOTE 不允许上传源代码和JavaDoc！！！
            // artifact(sourceJar)
            // artifact(javadocsJar)
            artifact("$buildDir/outputs/aar/$artifactId-release.aar")
            // 默认情况下不会将Dependencies写入POM，这段代码负责写
            toDependency(getPom(), configurations.publishCompile);
        }
    }
}
