apply plugin: 'maven-publish'
if (project.hasProperty("android")) {
    task androidJavadocs(type: Javadoc) {
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        if (project.hasProperty("android")) {
            android.libraryVariants.all { variant ->
                if (variant.name == 'release') {
                    owner.classpath += variant.javaCompileProvider.get().classpath
                }
            }
            exclude '**/R.html', '**/R.*.html', '**/index.html'
        }
    }
} else {
    task androidJavadocs(type: Javadoc) {
        source = sourceSets.main.allJava
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.hasProperty("android")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.allSource
    }
}


boolean useAndroidX = rootProject.properties.get("android.useAndroidX") == "true"
def VERSION_END = useAndroidX ? "androidx" : "support"


def getLocalProperties() {
    Properties localProperties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
    localProperties.load(inputStream)
    return localProperties
}

def getReleaseRepositoryUrl() {
    Properties localProperties = getLocalProperties()
    return localProperties.containsKey('RELEASE_REPOSITORY_URL') ? localProperties.get("RELEASE_REPOSITORY_URL") : ""
}

def getSnapshotRepositoryUrl() {
    Properties localProperties = getLocalProperties()
    return localProperties.containsKey('SNAPSHOT_REPOSITORY_URL') ? localProperties.get("SNAPSHOT_REPOSITORY_URL") : ""
}

def getRepositoryUsername() {
    Properties localProperties = getLocalProperties()
    return localProperties.containsKey('SONATYPE_NEXUS_USERNAME') ? localProperties.get("SONATYPE_NEXUS_USERNAME") : ""
}

def getRepositoryPassword() {
    Properties localProperties = getLocalProperties()
    return localProperties.containsKey('SONATYPE_NEXUS_PASSWORD') ? localProperties.get("SONATYPE_NEXUS_PASSWORD") : ""
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                if (project.hasProperty("android")) {
                    from components.getByName(VERSION_END + "Release")
                }
                artifact androidSourcesJar
                artifact androidJavadocsJar

                groupId = RP_GROUP
                artifactId = project.name
                version = RP_VERSION

                pom.withXml {
                    final root = asNode()
                    root.appendNode('name', project.name)
                    root.appendNode('scm').appendNode('url', RP_GIT_URL)
                }
            }
        }

        repositories {
            maven {
                url getReleaseRepositoryUrl()
                credentials {
                    username getRepositoryUsername()
                    password getRepositoryPassword()
                }
            }
        }
    }
}
