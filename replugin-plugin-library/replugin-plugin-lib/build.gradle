/*
 * Copyright (C) 2005-2017 Qihoo 360 Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed To in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

apply plugin: 'com.android.library'

def repluginVersion = "2.3.7"
def repluginVersionCode = 237
def repluginGroup = 'com.qihoo360.replugin'  // 组名

def repluginMinminSdkVersion = 1
def repluginTargetSdkVersion = 25
def repluginCompileSdkVersion = 25
def repluginBuildToolsVersion = '25.0.3'
def repluginSupportVersion = '23.4.0'

if (rootProject.ext.has('minSdkVersion')) {
    repluginMinminSdkVersion = rootProject.ext.minSdkVersion
}
if (rootProject.ext.has('compileSdkVersion')) {
    repluginCompileSdkVersion = rootProject.ext.compileSdkVersion
}
if (rootProject.ext.has('targetSdkVersion')) {
    repluginTargetSdkVersion = rootProject.ext.targetSdkVersion
}
if (rootProject.ext.has('buildToolsVersion')) {
    repluginBuildToolsVersion = rootProject.ext.buildToolsVersion
}
if (rootProject.ext.has('supportVersion')) {
    repluginSupportVersion = rootProject.ext.supportVersion
} else if (rootProject.ext.has('SUPPORT_LIBRARY_VERSION')) {
    repluginSupportVersion = rootProject.ext.SUPPORT_LIBRARY_VERSION
}

def useAndroidX = false
if (rootProject.ext.has('useAndroidX')) {
    useAndroidX = rootProject.ext.useAndroidX
}

android {
    compileSdkVersion repluginCompileSdkVersion
    buildToolsVersion repluginBuildToolsVersion

    defaultConfig {
        minSdkVersion repluginMinminSdkVersion
        targetSdkVersion repluginTargetSdkVersion
        versionCode repluginVersionCode
        versionName repluginVersion
        consumerProguardFiles 'replugin-library-rules.pro'
    }

    sourceSets {
        main {
            if (useAndroidX) {
                java.srcDirs = ['src/main/java', 'src/androidx/java']
            } else {
                java.srcDirs = ['src/main/java', 'src/support/java']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    //compile fileTree(dir: 'libs', include: ['*.jar'])
    if (useAndroidX) {
        compileOnly "androidx.appcompat:appcompat:1.1.0"
    } else {
        compileOnly "com.android.support:appcompat-v7:$repluginSupportVersion"
    }
}
